#r "paket: nuget  Fake.JavaScript.Npm //"
#r "paket: nuget Fake.Core.Target //"
#r "paket: nuget Thoth.Json.Net //"
#load "./.fake/build.fsx/intellisense.fsx"

open System.Text
open Fake.Core
open Fake.IO
open FileSystemOperators
open Globbing.Operators
open Thoth.Json.Net.Decode

let pwd = Shell.pwd()
let specName = "iml-socket-worker.spec"
let topDir = pwd @@ "_topdir"
let sources = topDir @@ "SOURCES"
let specs =  topDir @@ "SPECS"
let spec = specs @@ specName
let srpms = topDir @@ "SRPMS"
let buildDir = pwd @@ "targetdir"
let coprKey = pwd @@ "copr-key"

let cli = """
Build
Usage:
  prog [options]

Options:
  --prod                        Production build
  --release=NUM                 The release field for this build (defaults to 1)
  --copr-project=NAME           Copr Project
  --copr-login=LOGIN            Copr Login
  --copr-username=USERNAME      Copr Username
  --copr-token=TOKEN            Copr Token
"""

let ctx = Context.forceFakeContext()
let args = ctx.Arguments
let parser = Docopt(cli)
let parsedArguments = parser.Parse(args |> List.toArray)

let release =
  DocoptResult.tryGetArgument "--release" parsedArguments
  |> Option.defaultValue "1"

let isProd =
  DocoptResult.hasFlag "--prod" parsedArguments

let coprRepo =
  DocoptResult.tryGetArgument "--copr-project" parsedArguments
  |> Option.defaultValue "managerforlustre/manager-for-lustre-devel/"

let coprLogin =
  DocoptResult.tryGetArgument "--copr-login" parsedArguments

let coprUsername =
  DocoptResult.tryGetArgument "--copr-username" parsedArguments

let coprToken =
  DocoptResult.tryGetArgument "--copr-token" parsedArguments

module Option =
  let expect msg = function
    | Some x -> x
    | None -> failwith msg

let getPackageValue key decoder =
  Fake.IO.File.readAsString "package.json"
    |> decodeString (field key decoder)
    |> function
      | Ok x -> x
      | Error e ->
        failwithf "Could not find package.json %s, got this error %s" key e

Target.create "Clean" (fun _ ->
  Shell.cleanDirs [buildDir; topDir]
)

Target.create "Topdir" (fun _ ->
  Shell.mkdir topDir
  Shell.mkdir sources
  Shell.mkdir specs
)

Target.create "NpmBuild" (fun _ ->
  if isProd then
    let name = getPackageValue "name" string
    Fake.JavaScript.Npm.exec ("pack " + name) (fun o -> { o with WorkingDirectory = sources })
  else
    Fake.JavaScript.Npm.install(id)
    Fake.JavaScript.Npm.run "postversion" id
    Fake.JavaScript.Npm.exec ("pack " + pwd) (fun o -> { o with WorkingDirectory = sources })
)

Target.create "BuildSpec" (fun _ ->
  let v = getPackageValue "version" string

  Fake.IO.Templates.load [specName + ".template"]
    |> Fake.IO.Templates.replaceKeywords [("@version@", v)]
    |> Fake.IO.Templates.replaceKeywords [("@release@", release)]
    |> Seq.iter(fun (_, file) ->
      let x = UTF8Encoding()

      Fake.IO.File.writeWithEncoding x false spec (Seq.toList file)
    )
)

Target.create "SRPM" (fun _ ->
  let args = (sprintf "-bs --define \"_topdir %s\" %s" topDir spec)
  Shell.Exec ("rpmbuild", args)
    |> function
      | 0 -> ()
      | x -> failwithf "Got a non-zero exit code (%i) for rpmbuild %s" x args
)

Target.create "GenCoprConfig" (fun _ ->
  let login =
    coprLogin
    |> Option.expect "Could not find --copr-login"

  let username =
    coprUsername
    |> Option.expect "Could not find --copr-username"

  let token =
    coprToken
    |> Option.expect "Could not find --copr-token"

  Fake.IO.Templates.load ["copr.template"]
    |> Fake.IO.Templates.replaceKeywords [("@login@", login)]
    |> Fake.IO.Templates.replaceKeywords [("@username@", username)]
    |> Fake.IO.Templates.replaceKeywords [("@token@", token)]
    |> Seq.iter(fun (_, file) ->
      let x = UTF8Encoding()

      Fake.IO.File.writeWithEncoding x false coprKey (Seq.toList file)
    )
)

Target.create "Copr" (fun _ ->
  if not (File.exists coprKey) then
    failwithf "Expected copr key at: %s, it was not found" coprKey

  let path =
    !!(srpms @@ "*.src.rpm")
      |> Seq.tryHead
      |> function
        | Some x -> x
        | None -> failwith "Could not find SRPM"


  let args = sprintf "--config %s build %s %s" coprKey coprRepo path

  Shell.Exec ("copr-cli", args)
    |> function
      | 0 -> ()
      | x -> failwithf "Got a non-zero exit code (%i) for copr-cli %s" x args
)

open Fake.Core.TargetOperators

"Clean"
  ==> "Topdir"
  ==> "NpmBuild"
  ==> "BuildSpec"
  ==> "SRPM"
  ==> "GenCoprConfig"
  ==> "Copr"


// start build
Target.runOrDefaultWithArguments "Copr"
